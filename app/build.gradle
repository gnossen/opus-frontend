apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'

def beaconProps = new Properties()
rootProject.file('beacon.properties').withInputStream {
    stream -> beaconProps.load(stream)
}

for(p in beaconProps) {
    project.ext[p.key] = p.value
}

def getVersionString = { ->
    if(rootProject.hasProperty('versionNumber')) {
        return rootProject.ext.versionNumber
    } else {
        return '0.0.0'
    }
}

def getGitRevision = "git rev-parse --short HEAD".execute().text.trim()

android {

    lintOptions {
        abortOnError false
    }

    useLibrary 'org.apache.http.legacy'

    applicationVariants.all { variant ->

        // rename apks with version number and git revision
        variant.outputs.each { output ->
            output.outputFile = new File(
                output.outputFile.parent,
                output.outputFile.name.replace(".apk", "-${getVersionString()}-${getGitRevision}.apk").
                    replace("app", "${rootProject.projectName}"))
        }

        // delete intermediate unaligned apk
        variant.assemble.doLast {
           variant.outputs.each { output ->
                File aligned = output.outputFile;
                File unaligned = output.packageApplication.outputFile;

                if(!unaligned.getName().equalsIgnoreCase(aligned.getName())) {
                    unaligned.delete()
                }
            }
        }
    }

    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    signingConfigs {
        release
    }

    defaultConfig {
        applicationId "org.opus.beacon"
        minSdkVersion 21
        targetSdkVersion 23
        versionCode 1
        versionName "${getVersionString()}-${getGitRevision}"
        multiDexEnabled = true
    }

    buildTypes {
        debug {
            buildConfigField "String", "SERVER_CLIENT_ID", "\"${ServerClientID}\""
            buildConfigField "String", "SERVER_URL", "\"${ServerURL}\""
        }
        release {
            buildConfigField "String", "SERVER_CLIENT_ID", "\"${ServerClientID}\""
            buildConfigField "String", "SERVER_URL", "\"${ServerURL}\""
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    if(rootProject.file("beacon.properties").exists()) {
        buildTypes.release.signingConfig = signingConfigs.release
    }

    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/DEPENDENCIES'

    }
}

tasks.withType(Test) {
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        outputs.upToDateWhen { false }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.google.android.gms:play-services-auth:8.3.0'
    compile 'com.google.android.gms:play-services:8.3.0'
    compile 'com.android.support:appcompat-v7:23.0.0'
    compile "com.android.support:support-v13:23.1.0"
    compile "com.android.support:support-v4:23.1.0"
    compile 'org.springframework.android:spring-android-rest-template:2.0.0.M1'
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'org.apache.maven:maven-ant-tasks:2.1.3'
    testCompile 'org.robolectric:robolectric:3.0'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.6.1'
    // compile files('libs/httpclient-4.5.1.jar')
    compile files('libs/httpcore-4.4.3.jar')
    // compile files('libs/httpmime-4.5.1.jar')
    compile files('libs/mail.jar')

    compile('org.apache.httpcomponents:httpmime:4.3.6') {
        exclude module: 'httpclient'
    }

    compile 'org.apache.httpcomponents:httpclient-android:4.3.5'
}

repositories {
    maven {
        url 'http://repo.spring.io/milestone'
    }
}

if(rootProject.file("beacon.properties").exists()) {
    def props = new Properties()
    props.load(new FileInputStream(rootProject.file("beacon.properties")))
    android.signingConfigs.release.storeFile rootProject.file(props.keyStore)
    android.signingConfigs.release.storePassword props.keyStorePassword
    android.signingConfigs.release.keyAlias props.keyAlias
    android.signingConfigs.release.keyPassword props.keyAliasPassword
}
